buildscript {
    repositories {
        mavenCentral()
        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/th2'
        }
        mavenLocal()
    }
}
plugins {
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id "com.google.protobuf" version "0.8.15"
}

apply plugin: "java"
apply plugin: "java-library"

group = 'com.exactpro.test'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

ext {
    grpcVersion                 = '1.32.1'
    protobufVersion             = '3.13.0'
    serviceGeneratorVersion     = '3.1.5'

    genDir              = file("${project.rootDir}/src/gen")
    sharedDir           = file("${project.rootDir}/shared")
}

repositories {
    mavenCentral()
    maven {
        name 'Bintray'
        url 'https://dl.bintray.com/exactpro/th2'
    }
    mavenLocal()

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:2.10.1')

    api "com.exactpro.th2:grpc-common:3.0.1"

    api "com.google.protobuf:protobuf-java-util"
    api "io.grpc:grpc-stub"
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-core"
    implementation "io.grpc:grpc-netty"

    implementation "javax.annotation:javax.annotation-api:1.3.2"

    implementation "com.exactpro.th2:grpc-service-generator:${serviceGeneratorVersion}"
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/proto'
        }
        java {
            srcDirs 'src/gen/main/java', 'src/gen/main/grpc', 'src/gen/main/services/java/src'
        }
        resources {
            srcDirs 'src/gen/main/services/java/resources'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        services {
            artifact = "com.exactpro.th2:grpc-service-generator:${serviceGeneratorVersion}:all@jar"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = genDir
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            services {
                option 'javaInterfacesPath=./java/src'
                option 'javaInterfacesImplPath=./java/src'
                option 'javaMetaInfPath=./java/resources'
                option 'pythonPath=./python'
            }
        }
        ofSourceSet('main')
    }
}

compileJava.dependsOn.add('generateProto')

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    println("test_message signingKey:")
    println("test_message signingKey: $signingKey")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrh_username, password: ossrh_password)
      }

      snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrh_username, password: ossrh_password)
      }

      pom.project {
        name rootProject.name
        packaging 'jar'
        description ''
        url vcs_url

        scm {
          url vcs_url
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id developer_id
            name developer_name
            email developer_email
          }
        }
      }
    }
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = sharedDir
        }
//Nexus repo for publish from gitlab
        maven {
            name = 'nexusRepository'
            credentials {
                username = "${nexus_user}"
                password = "${nexus_password}"
            }
            url = "${nexus_url}"
        }
    }
}

clean {
    delete genDir
    delete sharedDir
}
